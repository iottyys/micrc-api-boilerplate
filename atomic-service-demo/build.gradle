plugins {
    id 'org.springframework.boot' version '2.3.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.palantir.docker' version '0.25.0'
    id 'java'
    id 'groovy'
    id 'idea'
}

group = 'info.ttyy.frame'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "Hoxton.SR9")
    set('groovyVersion', '2.4.15')
    set('camelVersion', '3.6.0')
    set('graphqlSpringVersion', '7.1.0')
    set('mybatisSpringVersion', '2.1.3')
    set('micrometerVersion', '1.5.5')
    set('graphqlRootPath', 'src/main/groovy/com/snszyk/iiot/lim/quota/infrastructure/ports/graphql')
}

task copyGraphql(type: Copy) {
    delete "$buildDir/resources/main/graphql"
    from "$graphqlRootPath"
    into "$buildDir/resources/main/graphql"
    include '**/*.graphql'
}

dependencyManagement {
    imports {
        //noinspection GrUnresolvedAccess
//        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.apache.camel.springboot:camel-spring-boot-dependencies:${camelVersion}"
    }
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    implementation "org.springframework.boot:spring-boot-starter-aop"

    // for camel
    implementation "org.apache.camel.springboot:camel-spring-boot-starter"

    // graphql
    implementation "com.graphql-java-kickstart:graphql-spring-boot-starter:${graphqlSpringVersion}"
    implementation "com.graphql-java-kickstart:graphiql-spring-boot-starter:${graphqlSpringVersion}"
    testImplementation "com.graphql-java-kickstart:graphql-spring-boot-starter-test:${graphqlSpringVersion}"

    // for prometheus, k8s probe
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    runtimeOnly "org.springframework.cloud:spring-cloud-starter-kubernetes-config"
    runtimeOnly "io.micrometer:micrometer-registry-prometheus:${micrometerVersion}"
    // for spring cloud stream message
//    implementation "org.springframework.cloud:spring-cloud-stream"
//    testImplementation "org.springframework.cloud:spring-cloud-stream-test-support"
    // for mybatis
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringVersion}"
    // for database
    runtimeOnly "com.h2database:h2"
    runtimeOnly "mysql:mysql-connector-java"
    // for lombok
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.name}:${project.version}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    dockerfile project.file('Dockerfile')
}

afterEvaluate {
    tasks.getByName('processResources').dependsOn(copyGraphql)
}
